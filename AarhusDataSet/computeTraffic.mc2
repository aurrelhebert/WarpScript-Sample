//
//   Copyright 2016  Cityzen Data
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//

<%
    'score' STORE
    'gtsFlow' STORE
    'gtsDensity' STORE
    
    //
    // Initialization res List
    //
    [] 'resTrafficScore' STORE
    [] 'resTrafficCount' STORE
    
    //
    // Compute GTS delta flow
    //
        
    [
        $gtsFlow
        mapper.delta
        2
        0
        0
    ]
    MAP
    'gtsDeltaFlow' STORE

    //
    // Compute GTS delta density 
    //
    
    [
        $gtsDensity
        mapper.delta
        2
        0
        0
    ]
    MAP
    'gtsDeltaDensity' STORE
    
    //
    // Keep only delta density value >=0
    //
    
    [ $gtsDeltaDensity 0.0 mapper.gt 0 0 0 ] MAP
    [ SWAP mapper.toboolean 0 0 0 ] MAP
    'gtsMaskDensity' STORE
    
    //
    // Apply a mask to kept only the flow where the points have a density value >=0
    //
    
    [
      $gtsMaskDensity
      $gtsDeltaFlow
      [ 'ext_id' ]
      op.mask
    ]
    APPLY
    'applyResult' STORE
    
    //
    // Keep only delta flow value <= score
    // GTS score is now on top of the stack
    //
        
    [ $applyResult $score mapper.lt 0 0 0 ] MAP
    
    //
    // Duplicate result to create GTS count on top of the stack
    //
    
    DUP
    [ SWAP bucketizer.count 0 STU 60 * 20 * 0 ] BUCKETIZE
%>
'computeTraffic' STORE